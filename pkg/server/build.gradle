// Copyright (c) 2018 Bhojpur Consulting Private Limited, India. All rights reserved.

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

plugins {
    id "java-library"
    id "net.bhojpur.ode.project" version "1.0.0"
    id "net.bhojpur.ode.gradle.icebuilder.slice" version "1.0.0"
}

group = "net.bhojpur.ode"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

ext {
    //the database type: must match the version set in ode-model
    databaseType = "psql"
    generatedDir = "build/${databaseType}"
}

dependencies {
    testImplementation("org.testng:testng:6.14.2")
    testImplementation("jmock:jmock:1.+")
    testImplementation("org.apache.directory.server:apacheds-all:1.5.7")
    testImplementation('net.bhojpur.ode:common-test:1.0.0')
    testImplementation('org.slf4j:slf4j-log4j12:1.5.0')

    // https://mvnrepository.com/artifact/org.apache.directory.shared/shared-ldap
    // testCompile group: 'org.apache.directory.shared', name: 'shared-ldap-constants', version: '1.0.0-M1'
    // testCompile group: 'org.apache.directory.shared', name: 'shared-ldap', version: '0.9.15'

    api('net.bhojpur.ode:renderer:1.0.0')

    // Spring framework stuff
    implementation("org.springframework:spring-context-support:4.3.14.RELEASE")
    implementation("org.springframework:spring-jms:4.3.14.RELEASE")
    implementation("org.springframework.security:spring-security-ldap:4.2.4.RELEASE")

    if (databaseType.equals("psql")) {
        // Postgres connect driver for java
        implementation 'org.postgresql:postgresql:42.2.1'
    }
    // Used by hibernate, see: https://stackoverflow.com/a/14365438
    implementation("org.javassist:javassist:3.25.0-GA")

    // API monitoring thing: http://jamonapi.sourceforge.net
    implementation("com.jamonapi:jamon:2.81")

    implementation("com.github.marcus-nl.btm:btm:3.0.0-mk1")

    implementation("org.apache.calcite:calcite-core:1.20.0")
    implementation("org.apache.commons:commons-collections4:4.1")
    implementation("org.apache.pdfbox:pdfbox:2.0.8")
    implementation("org.apache.xmlgraphics:batik-all:1.9.1")

    implementation("commons-beanutils:commons-beanutils:1.9.3")

    implementation("com.sun.mail:javax.mail:1.6.2")

    implementation("com.mortennobel:java-image-scaling:0.8.6")

    implementation("org.quartz-scheduler:quartz:2.2.1")

    implementation("org.subethamail:subethasmtp:3.1.7") {
        exclude group: 'javax.mail'
    }

    implementation("net.sf.ehcache:ehcache:2.10.4")

    implementation("commons-io:commons-io:2.6")
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    // enable TestNG support (default is JUnit)
    useTestNG() {
        suites "src/test/resources/unit.testng.xml"
    }
    dependsOn cleanTest
}

if (slice.iceVersion.contains("3.7")) {
    dependencies {
        api "com.zeroc:ice:3.7.2"
    }
    sourceSets.main.java {
        srcDirs "src/main/ice37", "$generatedDir/ice37"
    }
    slice.output = file("$generatedDir/ice37")
} else if (slice.iceVersion.contains("3.6")) {
    dependencies {
        api "com.zeroc:ice:3.6.5"
    }
    sourceSets.main.java {
        srcDirs "src/main/ice36", "$generatedDir/ice36"
    }
    slice.output = file("$generatedDir/ice36")
}

jar {
    manifest {
        attributes("Main-Class": 'ode.services.fulltext.Main')
    }
}

processResources {
    with copySpec {
        from 'src/main/resources'
        // Set version
        filter { String line ->
            line.startsWith('ode.version') ? 'ode.version='+version : line
        }
    }
}
